#
# Test Framework!
#

Test = Object.new(
  suites: []

  # All tests start with a `describe` block, which defines a test suite.
  # describe should be given a string `name` for the suite
  # and a block in `as` in which tests will be defined.
  describe: { |name, as|
    suite = Suite.new(name: name)
    as(suite.test)
    suites.push(suite)
  },
)

Suite = Object.new(
  tests: [],

  # Define an individual test.
  # If no block is given, this test is marked as "pending".
  test: { |name, is: null|
    tests.push(Unit.new(suite: self, name: name, test: is))
  },

  run: { |reporter|
    tests.each({ |test|
      reporter.startTest(test)

      test.test.do(
        ifTrue: { runOneTest(reporter, test: test) },
        ifFalse: { reporter.pending(test) }
      )

      reporter.endTest()
    })
  },

  runOneTest: { |reporter, test|
    try(
      {
        test.test(assert)
      },
      catch: { |error|
        reporter.errored(error)
      }
    )
  }

)

Unit = Object.new(

  # The string name given to this test.
  name: null,

  # The block of the test itself.
  test: null,

  # A back-reference to the suite this test is a member of.
  suite: null,
)

Runner = Object.new(
  suites: [],

  reporter: null,

  assert: { |expr, message: null|
    where = sender[0]
    expr.do(
      ifTrue: { reporter.success() },
      ifFalse: { reporter.failure(where, message: message || "Expected true, was false") }
    )
  },

  run: {
    reporter.start()

    suites.each({ |suite|
      suite.run(reporter)
    })

    reporter.complete()
  },
)

TestResult = Object.new(
  test: null,
  result: false,
  error: null,
  message: "",
  location: null,

  toString: {
    testDetails = test.suite.name + " " + test.name + ": " + message

    "\n" + testDetails + "\n\t" + testResult.location
  }
)

Reporter = Object.new(
  assertions: 0,

  successes: [],

  failures: [],

  pendingTests: [],

  startTime: 0,

  currentTest: null,

  start: {
    startTime = Time.utc()

    IO.puts("Running ")
    IO.puts()
  },

  startTest: { |test|
    currentTest = test
  },

  success: {
    assertions = assertions + 1
    successes.push(TestResult.new(test: currentTest, result: true))

    IO.print(".")
  },

  failure: { |location, message|
    assertions = assertions + 1
    failures.push(TestResult.new(test: currentTest, result: false, location: location, message: message))

    IO.print("F")
  },

  errored: { |error|
    assertions = assertions + 1
    failures.push(TestResult.new(test: currentTest, result: false, location: error.backtrace[0], message: error.message))

    IO.print("E")
  },

  endTest: {
    currentTest = null
  },

  pending: { |test|
    pendingTests.push(test)
  },

  complete: {
    IO.puts()

    endTime = Time.utc()
    elapsedMs = endTime - startTime

    hasPending = pendingTests.length() > 0

    # Output pending tests
    hasPending.do({
      IO.puts()
      IO.puts("Pending tests:")
      pendingTests.each({ |test|
        IO.puts()
        IO.puts("\t" + test.suite.name + " " + test.name)
      })
    })

    # Output failing test details
    (failures.length() > 0).do({
      IO.puts()
      IO.puts("Test failures:")
      failures.each({ |testResult|
        IO.puts(testResult.toString())
      })
    })

    results = "Finished in " + elapsedMs + "ms. "
    results = results + assertions + " assertions, "
    results = results + successes.length() + " successes, "
    results = results + failures.length() + " failures"

    hasPending.do({ results = results + ", " + pendingTests.length() + " pending" })

    IO.puts()
    IO.puts(results)
    IO.puts()

    # Return a boolean flag signaling a successful or failed test suite run
    failures.length() == 0
  }
)
